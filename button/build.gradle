/*
 * Copyright 2016, The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def packageVersion = '0.3'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.android.support:support-annotations:24.2.0'
    provided 'com.android.things:androidthings:0.1-preview'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    assert project.hasProperty('BRILLO_REPOSITORY'): \
       "Property BRILLO_REPOSITORY not set.\n" +
            "Please, set your ~/.gradle/init.gradle as instructed in go/brillo-gradle-setup"
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.google.androidthings.driver'
            artifactId 'button'
            version packageVersion
            artifacts = configurations.archives.artifacts
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url BRILLO_REPOSITORY
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
    }
}
